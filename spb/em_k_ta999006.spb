CREATE OR REPLACE PACKAGE BODY trp_mx_dl.em_k_ta999006
AS
  /* -------------------- DESCRIPCION --------------------
 || Acceso a la tabla ta999006
 || Procedimientos y funciones para el acceso de la tabla ta999006
 ||
 */ -----------------------------------------------------
 --
 /* -------------------- VERSION = 1.00 -------------------- */
 --
 /* -------------------- MODIFICACIONES -----------------
 || 2020/16/04 - JALUIS2 - v 1.00
 || Creacion del Package
 */ -----------------------------------------------------
 --
 /* --------------------------------------------------
 || Aqui comienza la declaracion de variables GLOBALES
 */ --------------------------------------------------
 --
 g_existe BOOLEAN := FALSE;
 --
 /* ---------------------------------------------------
 || Aqui comienza la declaracion de constantes GLOBALES
 */ ---------------------------------------------------
 --
 g_k_ini_corchete CONSTANT VARCHAR2(2) := ' [';
 g_k_fin_corchete CONSTANT VARCHAR2(1) := ']';
 --
 /* -------------------------------------------
 || Definicion de Globales Privadas al Package
 */ -------------------------------------------
 --
 -- ------------------------------------------------------------
 --
 /* ----------------------------------------------------
 || Aqui comienza la declaracion de subprogramas LOCALES
 */ ----------------------------------------------------
 --
 -- ------------------------------------------------------------
 --
 /**
 || Comprueba que se haya leido informacion
 */
 PROCEDURE pp_comprueba_error
 IS
    l_t_valores         tron2000.trn_k_ptd.t_t_valores;
 BEGIN
    --
    IF NOT g_existe
    THEN
       --
       l_t_valores(1) := g_k_ini_corchete || 'PK ta999006' || g_k_fin_corchete;
       --
       tron2000.trn_k_ptd.p_gen_error(p_cod_idioma  => tron2000.trn_k_ptd.f_dev_cod_idioma,
                                      p_cod_mensaje => 20001                              ,
                                      p_t_valores   => l_t_valores                        ,
                                      p_pre_mensaje => tron2000.trn_k_ptd.NULO            ,
                                      p_pos_mensaje => '[TIPO VEHICULO]'                  ); 
       --
    END IF;
    --
 END pp_comprueba_error;
 --
 /* -------------------------------------------
 || Procedimientos Privados
 */ -------------------------------------------
 --
 --
 --
 /* --------------------------------------------------------
 || Aqui comienza la declaracion de subprogramas del PACKAGE
 */ --------------------------------------------------------
 --
 -- ------------------------------------------------------------
 --
 /* -------------------------------------------
 || Procedimientos Publicos
 */ -------------------------------------------
 --
 /**
 || Lee un registro por PK y recupera valores vigentes de lo
 || particular a lo generico
 */
 FUNCTION f_lee_vigente(
                        p_cod_cia              ta999006.cod_cia             %TYPE,
                        p_cod_sector           ta999006.cod_sector          %TYPE,
                        p_cod_ramo             ta999006.cod_ramo            %TYPE,
                        p_num_poliza_grupo     ta999006.num_poliza_grupo    %TYPE,
                        p_num_contrato         ta999006.num_contrato        %TYPE,
                        p_num_subcontrato      ta999006.num_subcontrato     %TYPE,
                        p_cod_canal3           ta999006.cod_canal3          %TYPE,
                        p_cod_producto         ta999006.cod_producto        %TYPE,
                        p_cod_amb_circulacion  ta999006.cod_amb_circulacion %TYPE,
                        p_cod_tip_vehi         ta999006.cod_tip_vehi        %TYPE,
                        p_fec_validez          ta999006.fec_validez         %TYPE)
             RETURN ta999006%ROWTYPE
 IS
    --
    e_mca_inh         EXCEPTION                     ;
    l_t_valores       tron2000.trn_k_ptd.t_t_valores;
    l_reg             ta999006%ROWTYPE              ;
    --
    CURSOR cl_ta999006
    IS
       --
       SELECT a.*
         FROM ta999006 a
        WHERE   a.cod_cia           =    p_cod_cia
          AND a.cod_sector          IN  (p_cod_sector, tron2000.dc_k_ptd.COD_SECTOR_GEN)  
          AND a.cod_ramo            IN  (p_cod_ramo, tron2000.em_k_ptd_gni.COD_RAMO_GEN)  
          AND a.num_poliza_grupo    IN  (p_num_poliza_grupo, tron2000.em_k_ptd_gni.NUM_POLIZA_GRUPO_GEN)  
          AND a.num_contrato        IN  (p_num_contrato, tron2000.em_k_ptd_gni.NUM_CONTRATO_GEN)  
          AND a.num_subcontrato     IN  (p_num_subcontrato, tron2000.em_k_ptd_gni.NUM_SUBCONTRATO_GEN)  
          AND a.cod_canal3          IN  (p_cod_canal3, tron2000.dc_k_ptd.COD_CANAL3_GEN)  
          AND a.cod_producto        =    p_cod_producto  
          AND a.cod_amb_circulacion =    p_cod_amb_circulacion  
          AND a.cod_tip_vehi        IN  (p_cod_tip_vehi, tron2000.em_k_ptd_rsk_mot.COD_TIP_VEHI_GEN)  
          AND a.fec_validez         =   (SELECT MAX(fec_validez)  
                                           FROM ta999006  
                                          WHERE cod_cia             = a.cod_cia  
                                            AND cod_sector          = a.cod_sector  
                                            AND cod_ramo            = a.cod_ramo   
                                            AND num_poliza_grupo    = a.num_poliza_grupo   
                                            AND num_contrato        = a.num_contrato   
                                            AND num_subcontrato     = a.num_subcontrato   
                                            AND cod_canal3          = a.cod_canal3   
                                            AND cod_producto        = a.cod_producto   
                                            AND cod_amb_circulacion = a.cod_amb_circulacion   
                                            AND cod_tip_vehi        = a.cod_tip_vehi   
                                            AND fec_validez        <= p_fec_validez   
                                         )
        ORDER BY a.cod_sector, a.cod_ramo, a.num_poliza_grupo, a.num_contrato, a.num_subcontrato, a.cod_canal3, a.cod_tip_vehi;
    --
 BEGIN
    --
    OPEN        cl_ta999006           ;
    FETCH       cl_ta999006 INTO l_reg;
    g_existe := cl_ta999006 %FOUND    ;
    CLOSE       cl_ta999006           ;
    --
    pp_comprueba_error;
    --
    IF l_reg.mca_inh = tron2000.trn_k_ptd.SI
    THEN
       --
       RAISE e_mca_inh;
       --
    END IF;
    --
    RETURN l_reg;
    --
 EXCEPTION
    WHEN e_mca_inh
    THEN
       --
       tron2000.trn_k_ptd.p_gen_error(p_cod_idioma  => tron2000.trn_k_ptd.f_dev_cod_idioma,
                                      p_cod_mensaje => 20020                              ,
                                      p_t_valores   => l_t_valores                        ,
                                      p_pre_mensaje => tron2000.trn_k_ptd.NULO            ,
                                      p_pos_mensaje => '[TIPO VEHICULO]'                  );
       --
 END f_lee_vigente;
 --
END em_k_ta999006;
/
